apply plugin: 'checkstyle'              // https://docs.gradle.org/3.3/userguide/checkstyle_plugin.html
apply plugin: 'findbugs'    // https://docs.gradle.org/3.3/userguide/findbugs_plugin.html
apply plugin: 'pmd'                     // https://docs.gradle.org/3.3/userguide/pmd_plugin.html

def qualityDir = "$project.rootDir/quality/";
def reportsDir = "$project.buildDir/reports"

check.dependsOn 'checkFileNames', 'checkCodeStyle', 'findBugs', 'pmdCheck'

// http://checkstyle.sourceforge.net/
checkstyle {
    toolVersion '7.8.2'
    showViolations true
}

// https://pmd.github.io/
pmd {
    toolVersion = '5.8.1'
}

task checkFileNames(type: ParallelCheckstyle, group: 'Verification', description: 'Check file naming convention.') {
    configFile file("$qualityDir/checkstyle/naming_convention.xml")
    source 'src'
    include '**/*.java'
    include '**/*.xml'
    include '**/*.png'
    include '**/*.jpg'
    include '**/*.webp'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        xml {
            destination "$reportsDir/checkstyle/check_file_names.xml"
        }
    }

    classpath = files()
}

task checkCodeStyle(type: ParallelCheckstyle, group: 'Verification', description: 'Run Checkstyle') {
    configFile file("$qualityDir/checkstyle/google_checks.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        xml {
            destination "$reportsDir/checkstyle/check_code_style.xml"
        }
    }

    classpath = files()
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('compile') && task.name.endsWith('Sources')) {
        tasks.getByName('findBugs').dependsOn task
    }
}

// http://findbugs.sourceforge.net/
task findBugs(type: ParallelFindBugs,
    group: 'Verification',
    description: 'Run FindBugs') {

    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("$qualityDir/findbugs/android-exclude-filter.xml")
    classes = files("$project.projectDir/build/intermediates/classes")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        // XML and HTML cannot be used at the same time
        xml.enabled = false
        html.enabled = !xml.isEnabled()
        xml {
            destination "$reportsDir/findbugs/findbugs.xml"
            withMessages true
        }
        html {
            destination "$reportsDir/findbugs/findbugs.html"
        }
    }

    classpath = files()
}


task pmdCheck(type: ParallelPmd, group: 'Verification', description: 'Run PMD') {
    ruleSetFiles = files("$qualityDir/pmd/pmd-ruleset.xml")
    ignoreFailures = false
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        html.enabled = true
        xml {
            destination "$reportsDir/pmd/pmd.xml"
        }
        html {
            destination "$reportsDir/pmd/pmd.html"
        }
    }
}

// Reference: https://medium.com/@dpreussler/speed-up-your-android-gradle-build-baa329cdb836

@ParallelizableTask
class ParallelCheckstyle extends Checkstyle {
}

@ParallelizableTask
class ParallelFindBugs extends FindBugs {
}

@ParallelizableTask
class ParallelPmd extends Pmd {
}